\= import-example.cene
\= Copyright 2016, 2021 Ross Angle. Released under the MIT License.

\= You can execute this file like so at the command line:
\=
\=   $ cene demos/import-example.cene \
\=       --in demos/import-example-src \
\=       --out dist/demos/import-example
\=
\= This Cene program creates its output path as a directory, creates
\= the file hello-world.txt under that directory, and writes text to
\= that file that's obtained from a (defn hello-world - ...) defined
\= in the file import-example-src.cene under its input path.
\=
\= That example command line specifies the output directory as
\= dist/demos/import-example, so the file will be located at
\= dist/demos/import-example/hello-world.txt. It specifies the input
\= directory as demos/import-example-src, so the original file will be
\= read from demos/import-example-src/import-example-src.cene.


(def-macro import-the-import-example-src-file
  home-scope caller-scope my-stx-details args then
  
  (basic-nil-macro home-scope caller-scope my-stx-details then
  /fn caller-scope s mac
  /get-mode/fn mode
  /cast
    (foldr
      (read-all-force/input-path-blob-utf-8 mode
      /input-path-get (cli-input-directory mode)
        str.\;qq[import-example-src.cene])
      (folding caller-scope /no-effects/nil)
    /fn expr state
      (cast state folding scope launch-rest
        err.\;qq[Internal error]
      /folding (scope-get str.rest scope)
      /join-effects launch-rest
      /compile-expression-later (scope-get str.first scope) expr /fn -
      /no-effects/nil))
    folding scope launch-rest
    err.\;qq[Internal error]
    
    launch-rest))

(import-the-import-example-src-file)

\= Now that we've imported that file, the (hello-world -) function is
\= available for use.

(def-macro go home-scope caller-scope my-stx-details args then
  (basic-nil-macro home-scope caller-scope my-stx-details then
  /fn caller-scope s mac
  /get-mode/fn mode
  /output-path-blob-utf-8
    (output-path-get (cli-output-directory mode)
      str.\;qq[hello-world.txt])
  /hello-world/nil))

(go)
